#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	// регистр ВКМ_ВыполненныеКлиентуРаботы
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СтоимостьЧасаРаботы КАК ДоговорВКМ_СтоимостьЧасаРаботы,
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК ЧасыКОплатеКлиенту,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ПериодДействияНачала КАК ПериодДействияНачала,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ПериодДействияКонец КАК ПериодДействияКонец,
		|	ВКМ_ОбслуживаниеКлиента.Дата КАК Дата,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора КАК ВидДоговора
		|ПОМЕСТИТЬ ВТ_Документ
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = ВКМ_ОбслуживаниеКлиента.Ссылка
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиента.Специалист,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СтоимостьЧасаРаботы,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ПериодДействияНачала,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_ПериодДействияКонец,
		|	ВКМ_ОбслуживаниеКлиента.Дата,
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВидДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
		|ПОМЕСТИТЬ ВТ_Регистр
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Специалист) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Документ.ДоговорВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
		|	ВТ_Документ.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту,
		|	ВТ_Регистр.ПроцентОтРабот КАК ПроцентОтРабот,
		|	ВТ_Документ.ПериодДействияНачала КАК ПериодДействияНачала,
		|	ВТ_Документ.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ВТ_Документ.Дата КАК Дата,
		|	ВТ_Документ.ВидДоговора КАК ВидДоговора
		|ИЗ
		|	ВТ_Документ КАК ВТ_Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Регистр КАК ВТ_Регистр
		|		ПО ВТ_Документ.Специалист = ВТ_Регистр.Сотрудник";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Специалист", Специалист);
	 
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ВыборкаДетальныеЗаписи.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			   
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Документ не может быть проведен, договор должен быть с типом абонентское обслуживание"; 
				Сообщение.Сообщить(); 
				
			   Отказ = Истина;
			    
			КонецЕсли;
			
			Если ВыборкаДетальныеЗаписи.Дата <= ВыборкаДетальныеЗаписи.ПериодДействияНачала 
			         ИЛИ ВыборкаДетальныеЗаписи.Дата >= ВыборкаДетальныеЗаписи.ПериодДействияКонец Тогда
			         	
			     Сообщение = Новый СообщениеПользователю;
				 Сообщение.Текст = "Документ не может быть проведен, необходимо проверить сроки действия договора"; 
				 Сообщение.Сообщить(); 
				
			    Отказ = Истина;	
			         	
			КонецЕсли; 
	
			Если ВыборкаДетальныеЗаписи.ПроцентОтРабот = Null Тогда
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
				"Сотруднику %1 не указан процент от работ.", Специалист));
				Отказ = Истина;
			Возврат;
				
			Иначе
	
				Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
				Движение.Период = Дата;
				
				Движение.Клиент = Клиент;
				Движение.Договор = Договор;
				Движение.КоличествоЧасов = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту;
				Движение.СуммакОплате = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту * ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы;
				
				Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
				Движение.Период = Дата;  
				Движение.Сотрудник = Специалист;
				Движение.ЧасовКОплате = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту;
				Движение.СуммакОплате = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту * ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы * ВыборкаДетальныеЗаписи.ПроцентОтРабот / 100;
			КонецЕсли;
			
		КонецЦикла;
	 
КонецПроцедуры
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, 
		"ДатаПроведенияРабот, ВремяНачалаРабот, ВремяОкончанияРабот, Специалист"); 
	
	Если РеквизитыОбъекта.ДатаПроведенияРабот <> ДатаПроведенияРабот
		Или РеквизитыОбъекта.ВремяНачалаРабот <> ВремяНачалаРабот
		Или РеквизитыОбъекта.ВремяОкончанияРабот  <> ВремяОкончанияРабот
		Или РеквизитыОбъекта.Специалист <> Специалист Тогда 
		ЗаказИзменен = Истина;
	Иначе
		ЗаказИзменен = Ложь;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЗаказИзменен", ЗаказИзменен);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

КонецПроцедуры
	
Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеСвойства.ЭтоНовый Тогда

		Сообщение = СтрШаблон("Новый заказ №%1. Специалист %2. Дата проведения работ: %3. Время выполнения работ с %4 по %5", Номер, Специалист,
			Формат(ДатаПроведенияРабот, "ДЛФ=D"), Формат(ВремяНачалаРабот, "ДЛФ=T"), Формат(
			ВремяОкончанияРабот, "ДЛФ=T"));
		ДокОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		ДокОбъект.ТекстСообщения = Сообщение;
		ДокОбъект.Записать();

	ИначеЕсли ДополнительныеСвойства.ЗаказИзменен Тогда

		Сообщение = СтрШаблон("Заказ №%1 был изменен. Специалист %2. Дата проведения работ: %3. Время выполнения работ с %4 по %5", Номер,
			Специалист, Формат(ДатаПроведенияРабот, "ДЛФ=D"), Формат(ВремяНачалаРабот, "ДЛФ=T"), Формат(
			ВремяОкончанияРабот, "ДЛФ=T"));
		ДокОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		ДокОбъект.ТекстСообщения = Сообщение;
		ДокОбъект.Записать();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
#КонецЕсли


