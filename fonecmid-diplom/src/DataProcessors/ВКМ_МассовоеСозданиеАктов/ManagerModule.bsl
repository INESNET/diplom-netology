 #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
 
#Область СлужебныеПроцедурыИФункции

Функция СозданиеСпискаНаСервере(Дата, СоздатьОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	               |	ДоговорыКонтрагентов.Владелец КАК Владелец,
	               |	ДоговорыКонтрагентов.Организация КАК Организация
	               |ПОМЕСТИТЬ ВТ_ДанныеДоговоры
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	&Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ПериодДействияНачала И ДоговорыКонтрагентов.ВКМ_ПериодДействияКонец
	               |	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	               |	РеализацияТоваровУслуг.Договор КАК Договор
	               |ПОМЕСТИТЬ ВТ_ДокументРеализации
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачало И &ДатаОкончание
	               |	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДокументРеализации.Ссылка КАК Документ,
	               |	ВТ_ДанныеДоговоры.Владелец КАК Контрагент,
	               |	ВТ_ДанныеДоговоры.Организация КАК Организация,
	               |	ВТ_ДанныеДоговоры.Ссылка КАК Договор
	               |ИЗ
	               |	ВТ_ДанныеДоговоры КАК ВТ_ДанныеДоговоры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументРеализации КАК ВТ_ДокументРеализации
	               |		ПО ВТ_ДанныеДоговоры.Ссылка = ВТ_ДокументРеализации.Договор";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ДатаНачало", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончание", КонецМесяца(Дата));

	Выборка = Запрос.Выполнить().Выбрать();

	МассивДокументов = Новый Массив;

	Пока Выборка.Следующий() Цикл
		ДокументыРеализации = Новый Структура;
		Если СоздатьОбъект Тогда
			Если НЕ ЗначениеЗаполнено(Выборка.Документ) Тогда
				НоваяРеализация = СозданиеРеализации(Выборка, КонецМесяца(Дата));
				ДокументыРеализации.Вставить("Договор", Выборка.Договор);
				ДокументыРеализации.Вставить("Документ", НоваяРеализация);
			Иначе
				ДокументыРеализации.Вставить("Договор", Выборка.Договор);
				ДокументыРеализации.Вставить("Документ", Выборка.Документ);
			КонецЕсли;
		Иначе
			ДокументыРеализации.Вставить("Договор", Выборка.Договор);
			ДокументыРеализации.Вставить("Документ", Выборка.Документ);
		КонецЕсли;

		МассивДокументов.Добавить(ДокументыРеализации);

	КонецЦикла;

	Возврат МассивДокументов;

КонецФункции

Функция СозданиеРеализации(Выборка, Дата)

	НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
	НовыйДок.Дата = Дата;
	НовыйДок.Договор = Выборка.Договор;
	НовыйДок.Контрагент = Выборка.Контрагент;
	НовыйДок.Организация = Выборка.Организация;
	НовыйДок.ВыполнитьАвтозаполнение();
	
	Если НовыйДок.ПроверитьЗаполнение() Тогда

		Попытка  
			НачатьТранзакцию();
			НовыйДок.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбщегоНазначения.СообщитьПользователю("Проведение документа невозможно");
			ЗаписьЖурналаРегистрации("ОБРАБОТКА: Массовое создание актов отменено");
		КонецПопытки;

	Иначе
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
			"Не удалось создать документ реализации по Договору: %1. Не все обязательные данные заполнены", Выборка.Договор));
	КонецЕсли;

	Возврат НовыйДок.Ссылка;

КонецФункции

#КонецОбласти
#КонецЕсли